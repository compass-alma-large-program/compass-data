! Reduction pipeline for the line and continuum observations

begin procedure redu_init

    ! Define user parameters
    
    if .not.exist(redu) then

	define structure redu /global
	
	define char*16 redu%basename /global
	define char*8 redu%setting /global
	define char*8 redu%spws[4] /global
	
	define logical redu%extract_continuum /global
	define logical redu%self_calibrate /global
	define logical redu%delete_intermediate_files /global
	define logical redu%interactive /global

	let redu%spws 25 27 29 31
	let redu%extract_continuum .true.
	let redu%self_calibrate .true.
	let redu%delete_intermediate_files .true.
	let redu%interactive .false.
	
    endif
    
end procedure redu_init	


begin procedure redu_run

    ! Run the reduction pipeline

    define char*32 datadir
    define char*256 filelist /like redu%spws
    define int ispw

    let datadir "../../../../data/"
    let map_robust 1
    let map_size 500 500
    let map_cell 0.064 0.064

    ! FIXME: The default value has changed in v3.7-04. Set it by hand until this is fixed.
    let clean_nkeep 70

    let ispw 0
    for spw /in redu%spws

	let name 'redu%basename'"-"'redu%setting'"-spw"'spw'
	
	! Convert the UVFITS files to UVT
	@ fits_to_uvt 'datadir'"/calibrated/uvfits/"'name'".uvfits" 'datadir'"/reduced/"'redu%basename'"/"'name'".uvt"
	let name 'datadir'"/reduced/"'redu%basename'"/"'name'
	read uv 'name'
	if redu%interactive pause
	
	! Self-calibration
	if redu%self_calibrate then
	    let self_display .false.
	    let self_flag .true.
	    let self_times 10 10 /resize
	    ! XXX: Redefine pause when redu%interactive is .false. ?
	    if .not.redu%interactive symbol stop continue ! avoid pauses
	    selfcal phase
	    if redu%interactive pause
	    selfcal apply
	endif
	
	! Time-average
	uv_time
	
	! Save the UV table
	write uv 'name'
	let ispw ispw+1
	let filelist[ispw] 'name'
	
    next

    ! Merge the spectral windows
    let name 'datadir'"/reduced/"'redu%basename'"/"'redu%basename'"-"'redu%setting'
    if ispw.gt.1 then
	uv_merge 'name' /file filelist /mode concat
    else
	sic copy 'filelist[1]'".uvt" 'name'".uvt"
    endif
	
    ! Create UV tables for the lines and for the continuum
    read uv 'name'
    uv_preview
    if redu%interactive pause
    if redu%extract_continuum then
	uv_split /file 'name' 'name'"-lines" 'name'"-cont"
	! FIXME: uv_split ignores the path for the continuum table
	sic rename 'redu%basename'"-"'redu%setting'"-cont.uvt" 'datadir'"/reduced/"'redu%basename'"/"
    else
	uv_split /file 'name' 'name'"-lines"
    endif
	
    ! Make the continuum map and clean it
    if redu%extract_continuum then
	let name 'datadir'"/reduced/"'redu%basename'"/"'redu%basename'"-"'redu%setting'"-cont"
	read uv 'name'
	uv_map /continuum
	clean
	if redu%interactive pause
	primary
	write sky 'name'
    endif
	
    ! Make the line datacube cube and clean it
    let name 'datadir'"/reduced/"'redu%basename'"/"'redu%basename'"-"'redu%setting'"-lines"
    ! FIXME: Can we use less beams to speed-up the calculation and get a single dirty beam for all channels?
    let beam_step -1
    read uv 'name'
    uv_map
    clean
    if redu%interactive pause
    primary
    write sky 'name'
	
    ! Export the continuum UV table and image to FITS
    if redu%extract_continuum then
	let name 'datadir'"/reduced/"'redu%basename'"/"'redu%basename'"-"'redu%setting'"-cont"
	fits 'name'.fits from 'name'.lmv-sky /over
	system "gzip -f "'name'".fits"
	fits 'name'.uvfits from 'name'.uvt /over
	system "gzip -f "'name'".uvfits"
    endif

    ! Export the line UV table and datacube to FITS
    let name 'datadir'"/reduced/"'redu%basename'"/"'redu%basename'"-"'redu%setting'"-lines"
    fits 'name'.fits from 'name'.lmv-sky /over
    system "gzip -f "'name'".fits"
    fits 'name'.uvfits from 'name'.uvt /over
    system "gzip -f "'name'".uvfits"
	
    ! Remove intermediate files
    if redu%delete_intermediate_files then
	let name 'datadir'"/reduced/"'redu%basename'"/"'redu%basename'"-"'redu%setting'
	for spw /in redu%spws
	    sic delete 'name'"-spw"'spw'".uvt"
	next
	sic delete 'name'".uvt"
	sic delete 'name'"-cont.uvt"
	sic delete 'name'"-cont.lmv-sky"
	sic delete 'name'"-lines.uvt"
	sic delete 'name'"-lines.lmv-sky"
    endif
    
end procedure redu_run


begin procedure redu_cleanup

    ! Cleanup files and variables
    
    sic delete selfcal-PHASE.last
    sic delete selfcal-PHASE.last~
    sic delete selfcal.tmp
    sic delete test.gdf
    delete /var redu

end procedure redu_cleanup


! Main program

if "x&1".eq."xinit" then
    @ redu_init
else if "x&1".eq."xrun" then
    @ redu_run
else if "x&1".eq."xclean" then
    @ redu_cleanup
else
    message e redu "Usage: redu COMMAND"
    return base
endif
