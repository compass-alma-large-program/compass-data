! Reduce the line and continuum observations

sic parallel

define char*32 datadir
define char*16 basename
define char*8 setting
define char*8 spws[4]
define logical extract_continuum
define logical self_calibrate
define logical delete_intermediate_files

let datadir "../../../../data/"
let basename bhr71
let setting a
let spws 25 27 29 31
let extract_continuum .true.
let self_calibrate .true.
let delete_intermediate_files .true.
let map_robust 1
let map_size 500 500
let map_cell 0.064 0.064

! FIXME: The default value has changed in v3.7-04. Set it by hand until this is fixed.
let clean_nkeep 70

for spw /in spws

    let name 'basename'"-"'setting'"-spw"'spw'

    ! Convert the UVFITS files to UVT
    @ fits_to_uvt 'datadir'"/calibrated/uvfits/"'name'".uvfits" 'datadir'"/reduced/"'basename'"/"'name'".uvt"
    let name 'datadir'"/reduced/"'basename'"/"'name'
    read uv 'name'

    ! Self-calibration
    if self_calibrate then
	let self_display .false.
	let self_flag .true.
	let self_times 10 10 /resize
	selfcal phase
	selfcal apply
    endif

    ! Time-average
    uv_time

    ! Save the UV table
    write uv 'name'

    ! Create UV tables for the lines and for the continuum
    uv_preview
    if extract_continuum then
	uv_split /file 'name' 'name'"-lines" 'name'"-cont"
       ! FIXME: uv_split ignores the path for the continuum table
	sic rename 'basename'"-"'setting'"-spw"'spw'"-cont.uvt" 'datadir'"/reduced/"'basename'"/"
    else
	uv_split /file 'name' 'name'"-lines"
    endif

    ! Make the continuum map and clean it
    if extract_continuum then
	let name 'datadir'"/reduced/"'basename'"/"'basename'"-"'setting'"-spw"'spw'"-cont"
	read uv 'name'
	uv_map /continuum
	clean
	primary
	write sky 'name'
    endif

    ! Make the line datacube cube and clean it
    let name 'datadir'"/reduced/"'basename'"/"'basename'"-"'setting'"-spw"'spw'"-lines"
    read uv 'name'
    uv_map
    clean
    primary
    write sky 'name'

    ! Export the continuum UV table and image to FITS
    if extract_continuum then
	let name 'datadir'"/reduced/"'basename'"/"'basename'"-"'setting'"-spw"'spw'"-cont"
	fits 'name'.fits from 'name'.lmv-sky /over
	system "gzip -f "'name'".fits"
	fits 'name'.uvfits from 'name'.uvt /over
	system "gzip -f "'name'".uvfits"
    endif

    ! Export the line UV table and datacube to FITS
    let name 'datadir'"/reduced/"'basename'"/"'basename'"-"'setting'"-spw"'spw'"-lines"
    fits 'name'.fits from 'name'.lmv-sky /over
    system "gzip -f "'name'".fits"
    fits 'name'.uvfits from 'name'.uvt /over
    system "gzip -f "'name'".uvfits"

    ! Remove intermediate files
    if delete_intermediate_files then
	let name 'datadir'"/reduced/"'basename'"/"'basename'"-"'setting'"-spw"'spw'
	sic delete 'name'".uvt"
	sic delete 'name'"-cont.uvt"
	sic delete 'name'"-cont.lmv-sky"
	sic delete 'name'"-lines.uvt"
	sic delete 'name'"-lines.lmv-sky"
    endif
next

! Cleanup
sic delete selfcal-PHASE.last
sic delete selfcal-PHASE.last~
sic delete selfcal.tmp
sic delete test.gdf
