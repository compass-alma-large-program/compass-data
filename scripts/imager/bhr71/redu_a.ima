! Reduce the line and continuum observations

sic parallel

define char*32 datadir
define char*16 basename
define logical extract_continuum
define logical self_calibrate

let datadir "../../../data/"
let basename bhr71
let name 'basename'"-a"
let extract_continuum .true.
let self_calibrate .true.
let map_robust 0 ! natural weighting

! FIXME: The default value has changed in v3.7-04. Set it by hand until this is fixed.
let clean_nkeep 70

! Convert the UVFITS files to UVT
@ fits_to_uvt 'datadir'"/calibrated/uvfits/"'name'".uvfits" 'datadir'"/reduced/"'basename'"/"'name'".uvt"
let name 'datadir'"/reduced/"'basename'"/"'name'
read uv 'name'

! Self-calibration
if self_calibrate then
    let self_display .false.
    let self_flag .true.
    let self_times 10 10 /resize
    selfcal phase
    selfcal apply
endif

! Time-average
uv_time

! Extract the continuum
if extract_continuum then

    ! Save the original UV table
    write uv 'name'

    ! Filter channels with line emission
    uv_preview
    uv_filter

    ! Resample the UV table to reduce its size
    uv_compress /continuum

    ! Make the continuum map and clean it
    uv_map /continuum
    clean
    primary
    
    ! Save the continuum UV table and image
    write uv 'name'-cont
    write sky 'name'-cont

    ! Restore the original UV table
    read uv 'name'
endif

! Remove a baseline
uv_preview
uv_baseline

! Make the line datacube cube and clean it
uv_map
clean
primary

! Save the UV table and datacube
write uv 'name'
write sky 'name'

! Export the continuum UV table and image to FITS
if extract_continuum then
    fits 'name'-cont.fits from 'name'-cont.lmv-sky /over
    system "gzip -f "'name'"-cont.fits"
    fits 'name'-cont.uvfits from 'name'-cont.uvt /over
    system "gzip -f "'name'"-cont.uvfits"
endif

! Export the line UV table and datacube to FITS
fits 'name'.fits from 'name'.lmv-sky /over
system "gzip -f "'name'".fits"
fits 'name'.uvfits from 'name'.uvt /over
system "gzip -f "'name'".uvfits"

! Cleanup
sic delete selfcal-PHASE.last
sic delete selfcal-PHASE.last~
sic delete selfcal.tmp
sic delete test.gdf
