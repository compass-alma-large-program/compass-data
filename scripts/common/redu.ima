! Reduction pipeline for COMPASS line and continuum observations

begin procedure redu_init

    ! Define user parameters
    
    if .not.exist(redu) then

	define structure redu /global
	
	define char*16 redu%basename /global
	define char*8 redu%setting /global
	define char*8 redu%spws[4] /global
	define char*16 redu%subdir /global

	define integer redu%drop_edge_channels /global

	! Unused parameters, kept for backward compatibility
	define real redu%uv_preview_clip /global
	define integer redu%uv_preview_smooth /global
	define integer redu%uv_preview_taper /global
	define logical redu%uv_preview_save_plot /global

	define real redu%map_robust /global
	define real redu%map_size[2] /global
	define real redu%map_cell[2] /global
	define char*8 redu%map_continuum_method /global
	define real redu%primary_truncate /global
	
	define logical redu%extract_continuum /global
	define logical redu%self_calibrate /global
	define logical redu%uv_restore /global
	define logical redu%jvm_correction /global
	define logical redu%primary_beam_correction /global
	define logical redu%delete_intermediate_files /global
	define logical redu%interactive /global

	let redu%drop_edge_channels 5
	let redu%spws 25 27 29 31
	let redu%subdir "tests/contsub/"
	let redu%map_robust 1
	let redu%map_size 0 0
	let redu%map_cell 0 0
	let redu%map_continuum_method "scm"
	let redu%primary_truncate .2

	let redu%extract_continuum .true.
	let redu%self_calibrate .true.
	let redu%uv_restore .true.
	let redu%jvm_correction .false.
	let redu%primary_beam_correction .true.
	let redu%delete_intermediate_files .true.
	let redu%interactive .false.
	
    endif
    
end procedure redu_init	


begin procedure redu_run

    ! Run the reduction pipeline

    define char*32 datadir
    define char*16 image_ext

    let datadir "../../data/"
    let map_robust redu%map_robust
    let map_size redu%map_size
    let map_cell redu%map_cell
    
    for spw /in redu%spws

	say 'ANSI%YELLOW'"Reducing data for "'redu%basename'", setting "'redu%setting'", spectral window "'spw''ANSI%NONE'
	let name 'redu%basename'"-set"'redu%setting'"-spw"'spw'
	
	! Convert the UVFITS files to UVT
	sic mkdir 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/uvtables/"
	@ fits_to_uvt 'datadir'"/calibrated/uvfits/"'name'".uvfits" 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/uvtables/"'name'".uvt"
	let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/uvtables/"'name'
	read uv 'name' /range redu%drop_edge_channels -1*redu%drop_edge_channels CHANNEL
	if redu%interactive pause 'ANSI%YELLOW'"UV table loaded. You can check the result with UV_STAT and UV_PREVIEW, or type C to continue."'ANSI%NONE'
	
	! Self-calibration
	if redu%self_calibrate then
	    let self_display .false.
	    let self_flag .true.
	    let self_times 10 10 /resize
	    symbol stop continue ! avoid pauses at each self-calibration loop
	    selfcal phase
	    symbol stop pause
	    if redu%interactive pause 'ANSI%YELLOW'"Done with the self-calibration. You can adjust the self-calibration parameters, or type C to continue."'ANSI%NONE'
	    selfcal apply
	endif
	
	! Time-average
	uv_time
	
	! Save the UV table
	write uv 'name'
	
	! Make a line + continuum datacube and clean it
	! TODO: Clean the continuum and the lines separately.
	uv_map
	clean
	stat clean
	if redu%uv_restore then
	    if redu%jvm_correction fit /jvm
	    uv_restore
	endif
	if redu%interactive pause 'ANSI%YELLOW'"Done with the imaging. You can check the datacube with the VIEW command, or type C to continue."'ANSI%NONE'

	! Compute the continuum
	! TODO: Apply the primary beam correction
	map_continuum clean /method 'redu%map_continuum_method'
	clear
	stat continuum
	if redu%interactive pause 'ANSI%YELLOW'"Done with the continuum computation. You can check the continuum image  with the VIEW CONTINUUM command, or type C to continue."'ANSI%NONE'
	sic mkdir 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/cubes/"
	if redu%extract_continuum then
	    let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/cubes/"'redu%basename'"-set"'redu%setting'"-spw"'spw'"-cont"
	    write continuum 'name'
	endif

	! Subtract the continuum from the datacube
	map_combine clean SUBTRACT clean continuum
	if redu%interactive pause 'ANSI%YELLOW'"Done with the continuum subtraction. You can check the line datacube with the VIEW command, or type C to continue."'ANSI%NONE'
	let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/cubes/"'redu%basename'"-set"'redu%setting'"-spw"'spw'"-lines"
	if redu%primary_beam_correction then
	    primary /truncate redu%primary_truncate
	    let sky%rms clean%rms ! store the noise values from the clean image
	    let sky%noise clean%noise
	    write sky 'name'
	    let image_ext "lmv-sky"
	else
	    write clean 'name'
	    let image_ext "lmv-clean"
	endif

	! Export the UV table, the continuum image and the line data cube to FITS
	let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/uvtables/"'redu%basename'"-set"'redu%setting'"-spw"'spw'
	fits 'name'.uvfits from 'name'.uvt /over
	if redu%extract_continuum then
	    let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/cubes/"'redu%basename'"-set"'redu%setting'"-spw"'spw'"-cont"
	    ! FIXME: We should convert 'name'.'image_ext' instead
	    fits 'name'.fits from 'name'.lmv-clean /over
	endif
	let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/cubes/"'redu%basename'"-set"'redu%setting'"-spw"'spw'"-lines"
	fits 'name'.fits from 'name'.'image_ext' /over

	! Remove intermediate files
	if redu%delete_intermediate_files then
	    let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/uvtables/"'redu%basename'"-set"'redu%setting'"-spw"'spw'
	    sic delete 'name'".uvt"
	    let name 'datadir'"/reduced/"'redu%subdir'"/"'redu%basename'"/cubes/"'redu%basename'"-set"'redu%setting'"-spw"'spw'
	    ! FIXME: We should convert 'name'"-cont."'image_ext' instead
	    sic delete 'name'"-cont.lmv-clean"
	    sic delete 'name'"-lines."'image_ext'
	endif
    next

    say 'ANSI%YELLOW'"All done."'ANSI%NONE'
    
end procedure redu_run


begin procedure redu_cleanup

    ! Cleanup files and variables
    
    sic delete selfcal-PHASE.last
    sic delete selfcal-PHASE.last~
    sic delete selfcal.tmp
    sic delete test.gdf
    delete /var redu

end procedure redu_cleanup


! Main program

if "x&1".eq."xinit" then
    @ redu_init
else if "x&1".eq."xrun" then
    @ redu_run
else if "x&1".eq."xclean" then
    @ redu_cleanup
else
    message e redu "Usage: redu COMMAND"
    return base
endif
